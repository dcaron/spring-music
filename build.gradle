plugins {
    id 'org.springframework.boot' version '4.0.3'
    id 'java'
    id 'eclipse-wtp'
    id 'idea'
}

group = 'org.cloudfoundry.samples.music'

java {
    sourceCompatibility = '25'
}

repositories {
    mavenCentral()
}

ext {
    javaCfEnvVersion = '4.0.0'
}

dependencies {
    // Spring Boot
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
    implementation "org.springframework.boot:spring-boot-starter-data-redis"
    implementation "org.springframework.boot:spring-boot-starter-validation"

    // Java CfEnv
    implementation "io.pivotal.cfenv:java-cfenv-boot:${javaCfEnvVersion}"

    // JPA Persistence
    runtimeOnly "org.apache.commons:commons-pool2"
    runtimeOnly "com.h2database:h2"
    runtimeOnly "com.mysql:mysql-connector-j"
    runtimeOnly "org.postgresql:postgresql"
    runtimeOnly "com.microsoft.sqlserver:mssql-jdbc"

    // uncomment to use Lettuce instead of Jedis for Redis connections
    // runtime "io.lettuce:lettuce-core"

    // Webjars
    implementation "org.webjars:bootstrap:3.4.1"
    implementation "org.webjars:angularjs:1.8.3"
    implementation "org.webjars:angular-ui:0.4.0-3"
    implementation "org.webjars:angular-ui-bootstrap:0.14.3"
    implementation "org.webjars:jquery:3.7.1"

    // Oracle - uncomment one of the following after placing driver in ./libs
    // compile files('libs/ojdbc8.jar')
    // compile files('libs/ojdbc7.jar')

    // Testing
    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25

    if (JavaVersion.current() != JavaVersion.VERSION_25) {
        logger.warn("The build is using Java ${JavaVersion.current()} to build a Java ${JavaVersion.VERSION_25} compatible archive.")
        logger.warn("See the project README for instructions on changing the target Java version.")
    }
}

jar {
    enabled = false
}

test {
    useJUnitPlatform()
}

tasks.named("bootBuildImage") {
    builder = "paketobuildpacks/builder-jammy-base:latest"
}
